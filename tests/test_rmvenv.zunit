#!/usr/bin/env zunit

@setup {

    export DISABLE_AUTOSWITCH_VENV="1"
    load "../autoswitch_virtualenv.plugin.zsh"
    unset AUTOSWITCH_DEFAULT_REQUIREMENTS
    TARGET="$(mktemp -d)"
    cd "$TARGET"

    function poetry {
        echo 'poetry' $@
    }

    function pipenv {
        echo 'pipenv' $@
    }

    function deactivate {
        echo 'deactivate'
    }
}

@teardown {
    rm -rf "$TARGET"
}


@test 'rmvenv - shows warning if no .venv present' {
    run rmvenv

    assert $state equals 0
    assert "$output" same_as "No .venv file in the current directory!"
}


@test 'rmvenv - removes .venv if present' {
    # lightweight simulation of creating a virtualenv
    mkdir -p "$HOME/.virtualenvs/12345"

    echo "12345" > .venv

    run rmvenv

    assert $state equals 0
    assert "$output" same_as "Removing \e[35m12345\e[0m..."
    test ! -f .venv
    test ! -d "$HOME/.virtualenvs/12345"
}

@test 'rmvenv - removes .venv if present with function' {
    # lightweight simulation of creating a virtualenv
    mkdir -p "$HOME/.virtualenvs/54321"

    echo "54321" > .venv

    # Interfere with rm executable (in normal use alias or function would do the same).
    function rm(){ : }

    run rmvenv

    assert $state equals 0
    assert "$output" same_as "Removing \e[35m54321\e[0m..."
    test ! -f .venv
    test ! -d "$HOME/.virtualenvs/54321"
}

@test 'rmvenv - removes .venv and deactivates if currently active' {
    test_venv="$(basename $PWD)"
    run mkvenv
    target_venv="$(<.venv)"

    run rmvenv

    assert $state equals 0
    assert "$output" same_as "Removing \e[35m$target_venv\e[0m..."
    test ! -f .venv
    test ! -d "$HOME/.virtualenvs/$test_venv"
}

@test 'rmvenv - uninstalls poetry project' {
    touch "poetry.lock"

    run rmvenv

    assert $state equals 0
    assert "$lines[1]" same_as "deactivate"
    assert "$lines[2]" same_as "poetry env remove poetry run which python"
}


@test 'rmvenv - uninstalls pipenv project' {
    touch "Pipfile"

    run rmvenv

    assert $state equals 0
    assert "$lines[1]" same_as "deactivate"
    assert "$lines[2]" same_as "pipenv --rm"
}
